// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

    //the first mux16
    Mux16(a=instruction,b=aluout,sel=instruction[15],out=ain);

    //A register load bits
    Not(in=instruction[15], out=not-instruction15);
    Or(a=not-instruction15, b=instruction[5], out=loada);

    //A register operation
    ARegister(in=ain,load=loada,out[0..14]=addressM,out=aout);

    //second mux16 operation, instruction[12] check whether it's a register or m register
    Mux16(a=aout,b=inM,sel=instruction[12],out=muxout);

    //alu operation

    //alu control bits
    And(a=instruction[11],b=instruction[15],out=zxin);
    And(a=instruction[10],b=instruction[15],out=nxin);
    Or(a=instruction[9],b=not-instruction15,out=zyin);
    Or(a=instruction[8],b=not-instruction15,out=nyin);
    And(a=instruction[7],b=instruction[15],out=fin);
    And(a=instruction[6],b=instruction[15],out=noin);


    ALU(x=dout ,y=muxout ,zx=zxin ,nx=nxin ,zy=zyin ,ny=nyin ,f=fin ,no=noin ,out=outM, out=aluout, zr=zero ,ng=neg);

    //load bit for D register
    And(a=instruction[4],b=instruction[15],out=loadd);

    //D register operation
    DRegister(in=aluout,load=loadd,out=dout);

    //control bits for writeM
    And(a=instruction[15],b=instruction[3],out=writeM);

    //prepare for jump, pos is 1 if output is positive
    Or(a=zero,b=neg,out=notpos);
    Not(in=notpos,out=pos);

    And(a=instruction[0],b=pos,out=j3);//j3
    And(a=instruction[1],b=zero,out=j2);//j2
    And(a=instruction[2],b=neg,out=j1);//j1

    Or(a=j1,b=j2,out=j12);
    Or(a=j12,b=j3,out=j123);

    And(a=j123,b=instruction[15],out=jump);

    //pc
    PC(in=aout ,load=jump ,inc=true ,reset=reset ,out[0..14]=pc);
}